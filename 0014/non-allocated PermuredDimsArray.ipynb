{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "992facaa-5537-454d-ada0-ee05f60d92c0",
   "metadata": {},
   "source": [
    "https://github.com/JuliaLang/julia/blob/073af4a7415c6e9f15df29b6cf3732050f4ce7c8/base/permuteddimsarray.jl#L42"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "0d73b28f-5af2-4db4-8d62-00a7bd3d0334",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Main.My"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# This file is a part of Julia. License is MIT: https://julialang.org/license\n",
    "\n",
    "module My\n",
    "\n",
    "import Base: permutedims, permutedims!\n",
    "# export PermutedDimsArray\n",
    "\n",
    "# Some day we will want storage-order-aware iteration, so put perm in the parameters\n",
    "struct PermutedDimsArray{T,N,AA<:AbstractArray{T,N}} <: AbstractArray{T,N}\n",
    "    parent::AA\n",
    "    perm::NTuple{N,Int}\n",
    "    iperm::NTuple{N,Int}\n",
    "\n",
    "    function PermutedDimsArray{T,N,AA}(data::AA, perm::NTuple{N,Int}, iperm::NTuple{N,Int}) where {T,N,AA<:AbstractArray{T,N}}\n",
    "        isperm(perm) || throw(ArgumentError(string(perm, \" is not a valid permutation of dimensions 1:\", N)))\n",
    "        all(iperm[perm[d]]==d for d in 1:N) || throw(ArgumentError(string(perm, \" and \", iperm, \" must be inverses\")))\n",
    "        new(data, perm, iperm)\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    PermutedDimsArray(A, perm) -> B\n",
    "Given an AbstractArray `A`, create a view `B` such that the\n",
    "dimensions appear to be permuted. Similar to `permutedims`, except\n",
    "that no copying occurs (`B` shares storage with `A`).\n",
    "See also [`permutedims`](@ref), [`invperm`](@ref).\n",
    "# Examples\n",
    "```jldoctest\n",
    "julia> A = rand(3,5,4);\n",
    "julia> B = PermutedDimsArray(A, (3,1,2));\n",
    "julia> size(B)\n",
    "(4, 3, 5)\n",
    "julia> B[3,1,2] == A[1,2,3]\n",
    "true\n",
    "```\n",
    "\"\"\"\n",
    "function PermutedDimsArray(data::AbstractArray{T,N}, perm) where {T,N}\n",
    "    length(perm) == N || throw(ArgumentError(string(perm, \" is not a valid permutation of dimensions 1:\", N)))\n",
    "    iperm = invperm(perm)\n",
    "    PermutedDimsArray{T,N,typeof(data)}(data, (perm...,), (iperm...,))\n",
    "end\n",
    "\n",
    "Base.parent(A::PermutedDimsArray) = A.parent\n",
    "Base.size(A::PermutedDimsArray) = genperm(size(parent(A)), A.perm)\n",
    "Base.axes(A::PermutedDimsArray) = genperm(axes(parent(A)), A.perm)\n",
    "\n",
    "Base.similar(A::PermutedDimsArray, T::Type, dims::Base.Dims) = similar(parent(A), T, dims)\n",
    "\n",
    "Base.unsafe_convert(::Type{Ptr{T}}, A::PermutedDimsArray{T}) where {T} = Base.unsafe_convert(Ptr{T}, parent(A))\n",
    "\n",
    "# It's OK to return a pointer to the first element, and indeed quite\n",
    "# useful for wrapping C routines that require a different storage\n",
    "# order than used by Julia. But for an array with unconventional\n",
    "# storage order, a linear offset is ambiguous---is it a memory offset\n",
    "# or a linear index?\n",
    "Base.pointer(A::PermutedDimsArray, i::Integer) = throw(ArgumentError(\"pointer(A, i) is deliberately unsupported for PermutedDimsArray\"))\n",
    "\n",
    "function Base.strides(A::PermutedDimsArray{T,N}) where {T,N}\n",
    "    s = strides(parent(A))\n",
    "    ntuple(d->s[A.perm[d]], Val(N))\n",
    "end\n",
    "Base.elsize(::Type{<:PermutedDimsArray{<:Any, <:Any, P}}) where {P} = Base.elsize(P)\n",
    "\n",
    "@inline function Base.getindex(A::PermutedDimsArray{T,N}, I::Vararg{Int,N}) where {T,N}\n",
    "    @boundscheck checkbounds(A, I...)\n",
    "    @inbounds val = getindex(A.parent, genperm(I, A.iperm)...)\n",
    "    val\n",
    "end\n",
    "@inline function Base.setindex!(A::PermutedDimsArray{T,N}, val, I::Vararg{Int,N}) where {T,N}\n",
    "    @boundscheck checkbounds(A, I...)\n",
    "    @inbounds setindex!(A.parent, val, genperm(I, A.iperm)...)\n",
    "    val\n",
    "end\n",
    "\n",
    "@inline genperm(I::NTuple{N,Any}, perm::Dims{N}) where {N} = ntuple(d -> I[perm[d]], Val(N))\n",
    "@inline genperm(I, perm::AbstractVector{Int}) = genperm(I, (perm...,))\n",
    "\n",
    "\"\"\"\n",
    "    permutedims(A::AbstractArray, perm)\n",
    "Permute the dimensions of array `A`. `perm` is a vector or a tuple of length `ndims(A)`\n",
    "specifying the permutation.\n",
    "See also [`permutedims!`](@ref), [`PermutedDimsArray`](@ref), [`transpose`](@ref), [`invperm`](@ref).\n",
    "# Examples\n",
    "```jldoctest\n",
    "julia> A = reshape(Vector(1:8), (2,2,2))\n",
    "2×2×2 Array{Int64, 3}:\n",
    "[:, :, 1] =\n",
    " 1  3\n",
    " 2  4\n",
    "[:, :, 2] =\n",
    " 5  7\n",
    " 6  8\n",
    "julia> permutedims(A, (3, 2, 1))\n",
    "2×2×2 Array{Int64, 3}:\n",
    "[:, :, 1] =\n",
    " 1  3\n",
    " 5  7\n",
    "[:, :, 2] =\n",
    " 2  4\n",
    " 6  8\n",
    "julia> B = randn(5, 7, 11, 13);\n",
    "julia> perm = [4,1,3,2];\n",
    "julia> size(permutedims(B, perm))\n",
    "(13, 5, 11, 7)\n",
    "julia> size(B)[perm] == ans\n",
    "true\n",
    "```\n",
    "\"\"\"\n",
    "function permutedims(A::AbstractArray, perm)\n",
    "    dest = similar(A, genperm(axes(A), perm))\n",
    "    permutedims!(dest, A, perm)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    permutedims(m::AbstractMatrix)\n",
    "Permute the dimensions of the matrix `m`, by flipping the elements across the diagonal of\n",
    "the matrix. Differs from `LinearAlgebra`'s [`transpose`](@ref) in that the\n",
    "operation is not recursive.\n",
    "# Examples\n",
    "```jldoctest; setup = :(using LinearAlgebra)\n",
    "julia> a = [1 2; 3 4];\n",
    "julia> b = [5 6; 7 8];\n",
    "julia> c = [9 10; 11 12];\n",
    "julia> d = [13 14; 15 16];\n",
    "julia> X = [[a] [b]; [c] [d]]\n",
    "2×2 Matrix{Matrix{Int64}}:\n",
    " [1 2; 3 4]     [5 6; 7 8]\n",
    " [9 10; 11 12]  [13 14; 15 16]\n",
    "julia> permutedims(X)\n",
    "2×2 Matrix{Matrix{Int64}}:\n",
    " [1 2; 3 4]  [9 10; 11 12]\n",
    " [5 6; 7 8]  [13 14; 15 16]\n",
    "julia> transpose(X)\n",
    "2×2 transpose(::Matrix{Matrix{Int64}}) with eltype Transpose{Int64, Matrix{Int64}}:\n",
    " [1 3; 2 4]  [9 11; 10 12]\n",
    " [5 7; 6 8]  [13 15; 14 16]\n",
    "```\n",
    "\"\"\"\n",
    "permutedims(A::AbstractMatrix) = permutedims(A, (2,1))\n",
    "\n",
    "\"\"\"\n",
    "    permutedims(v::AbstractVector)\n",
    "Reshape vector `v` into a `1 × length(v)` row matrix.\n",
    "Differs from `LinearAlgebra`'s [`transpose`](@ref) in that\n",
    "the operation is not recursive.\n",
    "# Examples\n",
    "```jldoctest; setup = :(using LinearAlgebra)\n",
    "julia> permutedims([1, 2, 3, 4])\n",
    "1×4 Matrix{Int64}:\n",
    " 1  2  3  4\n",
    "julia> V = [[[1 2; 3 4]]; [[5 6; 7 8]]]\n",
    "2-element Vector{Matrix{Int64}}:\n",
    " [1 2; 3 4]\n",
    " [5 6; 7 8]\n",
    "julia> permutedims(V)\n",
    "1×2 Matrix{Matrix{Int64}}:\n",
    " [1 2; 3 4]  [5 6; 7 8]\n",
    "julia> transpose(V)\n",
    "1×2 transpose(::Vector{Matrix{Int64}}) with eltype Transpose{Int64, Matrix{Int64}}:\n",
    " [1 3; 2 4]  [5 7; 6 8]\n",
    "```\n",
    "\"\"\"\n",
    "permutedims(v::AbstractVector) = reshape(v, (1, length(v)))\n",
    "\n",
    "\"\"\"\n",
    "    permutedims!(dest, src, perm)\n",
    "Permute the dimensions of array `src` and store the result in the array `dest`. `perm` is a\n",
    "vector specifying a permutation of length `ndims(src)`. The preallocated array `dest` should\n",
    "have `size(dest) == size(src)[perm]` and is completely overwritten. No in-place permutation\n",
    "is supported and unexpected results will happen if `src` and `dest` have overlapping memory\n",
    "regions.\n",
    "See also [`permutedims`](@ref).\n",
    "\"\"\"\n",
    "function permutedims!(dest, src::AbstractArray, perm)\n",
    "    Base.checkdims_perm(dest, src, perm)\n",
    "    P = PermutedDimsArray(dest, invperm(perm))\n",
    "    _copy!(P, src)\n",
    "    return dest\n",
    "end\n",
    "\n",
    "function Base.copyto!(dest::PermutedDimsArray{T,N}, src::AbstractArray{T,N}) where {T,N}\n",
    "    checkbounds(dest, axes(src)...)\n",
    "    _copy!(dest, src)\n",
    "end\n",
    "Base.copyto!(dest::PermutedDimsArray, src::AbstractArray) = _copy!(dest, src)\n",
    "\n",
    "function _copy!(P::PermutedDimsArray, src)\n",
    "    # If dest/src are \"close to dense,\" then it pays to be cache-friendly.\n",
    "    # Determine the first permuted dimension\n",
    "    d = 0  # d+1 will hold the first permuted dimension of src\n",
    "    while d < ndims(src) && P.perm[d+1] == d+1\n",
    "        d += 1\n",
    "    end\n",
    "    if d == ndims(src)\n",
    "        copyto!(parent(P), src) # it's not permuted\n",
    "    else\n",
    "        R1 = CartesianIndices(axes(src)[1:d])\n",
    "        d1 = findfirst(isequal(d+1), perm)::Int  # first permuted dim of dest\n",
    "        R2 = CartesianIndices(axes(src)[d+2:d1-1])\n",
    "        R3 = CartesianIndices(axes(src)[d1+1:end])\n",
    "        _permutedims!(P, src, R1, R2, R3, d+1, d1)\n",
    "    end\n",
    "    return P\n",
    "end\n",
    "\n",
    "@noinline function _permutedims!(P::PermutedDimsArray, src, R1::CartesianIndices{0}, R2, R3, ds, dp)\n",
    "    ip, is = axes(src, dp), axes(src, ds)\n",
    "    for jo in first(ip):8:last(ip), io in first(is):8:last(is)\n",
    "        for I3 in R3, I2 in R2\n",
    "            for j in jo:min(jo+7, last(ip))\n",
    "                for i in io:min(io+7, last(is))\n",
    "                    @inbounds P[i, I2, j, I3] = src[i, I2, j, I3]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    P\n",
    "end\n",
    "\n",
    "@noinline function _permutedims!(P::PermutedDimsArray, src, R1, R2, R3, ds, dp)\n",
    "    ip, is = axes(src, dp), axes(src, ds)\n",
    "    for jo in first(ip):8:last(ip), io in first(is):8:last(is)\n",
    "        for I3 in R3, I2 in R2\n",
    "            for j in jo:min(jo+7, last(ip))\n",
    "                for i in io:min(io+7, last(is))\n",
    "                    for I1 in R1\n",
    "                        @inbounds P[I1, i, I2, j, I3] = src[I1, i, I2, j, I3]\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    P\n",
    "end\n",
    "\n",
    "function Base._mapreduce_dim(f, op, init::Base._InitialValue, A::PermutedDimsArray, dims::Colon)\n",
    "    Base._mapreduce_dim(f, op, init, parent(A), dims)\n",
    "end\n",
    "\n",
    "function Base.mapreducedim!(f, op, B::AbstractArray{T,N}, A::PermutedDimsArray{T,N}) where {T,N}\n",
    "    C = PermutedDimsArray{T,N,typeof(B)}(B, B.perm, B.iperm) # make the inverse permutation for the output\n",
    "    Base.mapreducedim!(f, op, C, parent(A))\n",
    "    B\n",
    "end\n",
    "\n",
    "function Base.showarg(io::IO, A::PermutedDimsArray{T,N}, toplevel) where {T,N}\n",
    "    print(io, \"PermutedDimsArray(\")\n",
    "    Base.showarg(io, parent(A), false)\n",
    "    print(io, \", \", A.perm, ')')\n",
    "    toplevel && print(io, \" with eltype \", eltype(A))\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "a4c7fa88-f700-4e5e-bb80-01d74bc20744",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  24.674 ns (0 allocations: 0 bytes)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "3×2×4 PermutedDimsArray(::Array{Int64, 3}, (2, 3, 1)) with eltype Int64:\n",
       "[:, :, 1] =\n",
       " 111  112\n",
       " 121  122\n",
       " 131  132\n",
       "\n",
       "[:, :, 2] =\n",
       " 211  212\n",
       " 221  222\n",
       " 231  232\n",
       "\n",
       "[:, :, 3] =\n",
       " 311  312\n",
       " 321  322\n",
       " 331  332\n",
       "\n",
       "[:, :, 4] =\n",
       " 411  412\n",
       " 421  422\n",
       " 431  432"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using BenchmarkTools\n",
    "x = 100(1:4) .+ 10(1:3)' .+ reshape(1:2, 1, 1, :)\n",
    "@btime p = My.PermutedDimsArray($x, (2, 3, 1))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a2b5e106-cc7f-4b8e-bff5-9162f753161c",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "ipynb,auto:hydrogen"
  },
  "kernelspec": {
   "display_name": "Julia 1.8.0-DEV",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
