{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "06dbb787-5235-4eeb-825f-7e78d058100f",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "O.getstate(XOS) = 0xcf5cb69b00e5796e\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "20-element Vector{Float64}:\n",
       " 0.8100084427286641\n",
       " 0.7945790302334755\n",
       " 0.4527537345030621\n",
       " 0.3883190569924725\n",
       " 0.7849263208397615\n",
       " 0.5494120941449921\n",
       " 0.9967663009734609\n",
       " 0.20769453475623612\n",
       " 0.6091616080484087\n",
       " 0.7785419203933468\n",
       " 0.45538790238573457\n",
       " 0.25460736078389556\n",
       " 0.012481761822934034\n",
       " 0.6496374599922397\n",
       " 0.7919924993864689\n",
       " 0.7456972440390945\n",
       " 0.7953481040013348\n",
       " 0.5830927997625643\n",
       " 0.6205491924943326\n",
       " 0.7756648510222477"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Random\n",
    "\n",
    "module O\n",
    "\n",
    "export TaskLocalXorshift64, XOS\n",
    "\n",
    "using Random: Random, AbstractRNG, RandomDevice, SamplerType, SamplerTrivial, CloseOpen01_64\n",
    "\n",
    "struct TaskLocalXorshift64 <: AbstractRNG end\n",
    "\n",
    "const XOS = TaskLocalXorshift64()\n",
    "\n",
    "@inline getstate(::TaskLocalXorshift64) = current_task().rngState0\n",
    "\n",
    "@inline setstate!(xos::TaskLocalXorshift64, seed::Integer) =\n",
    "    (current_task().rngState0 = mod(seed, UInt64); xos)\n",
    "\n",
    "@inline function Random.rand(xos::TaskLocalXorshift64, ::SamplerType{UInt64})\n",
    "    x = res = getstate(xos)\n",
    "    x ⊻= x << 13\n",
    "    x ⊻= x >> 7\n",
    "    x ⊻= x << 17\n",
    "    setstate!(xos, x)\n",
    "    res\n",
    "end\n",
    "\n",
    "@inline Random.rand(xos::TaskLocalXorshift64, ::SamplerTrivial{CloseOpen01_64}) =\n",
    "    Float64(rand(xos, UInt64) >>> 11) * 0x1.0p-53\n",
    "\n",
    "Random.seed!(xos::TaskLocalXorshift64) =\n",
    "    setstate!(xos, rand(RandomDevice(), UInt64))\n",
    "\n",
    "Random.seed!(xos::TaskLocalXorshift64, seed::Integer) =\n",
    "    setstate!(xos, seed)\n",
    "\n",
    "end\n",
    "\n",
    "using .O\n",
    "\n",
    "@show O.getstate(XOS)\n",
    "A = [rand(XOS) for _ in 1:20]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "7827d6d3-1491-4fa5-acd8-8f4a336f103e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "O.getstate(XOS) = 0xef6e5329f86ffb29\n",
      "O.getstate(XOS) = 0x0000000001234567\n"
     ]
    }
   ],
   "source": [
    "Random.seed!(XOS)\n",
    "@show O.getstate(XOS)\n",
    "Random.seed!(XOS, 0x1234567)\n",
    "@show O.getstate(XOS);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "78147619-cfcc-4ad6-8198-461c02e73cc7",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  3.363440 seconds\n",
      "  3.332102 seconds\n",
      "  3.303405 seconds\n",
      "  3.307343 seconds\n",
      "  3.292089 seconds\n",
      "  3.342178 seconds\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "3.141454796"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function mcpi(L=10^9, rng=XOS)\n",
    "    c = 0\n",
    "    for i in 1:L\n",
    "        c += rand(rng)^2 + rand(rng)^2 ≤ 1\n",
    "    end\n",
    "    4c/L\n",
    "end\n",
    "\n",
    "@time mcpi()\n",
    "@time mcpi()\n",
    "@time mcpi()\n",
    "@time mcpi()\n",
    "@time mcpi()\n",
    "@time mcpi()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "fbd5bf69-8f89-4380-822a-6cb96e69d9c7",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  0.427697 seconds\n",
      "  0.428507 seconds\n",
      "  0.446790 seconds\n",
      "  0.447515 seconds\n",
      "  0.448349 seconds\n",
      "  0.438973 seconds\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "3.1417432"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using LoopVectorization\n",
    "\n",
    "@inline isinside(i, rng=XOS) =\n",
    "    rand(rng)^2 + rand(rng)^2 ≤ 1\n",
    "\n",
    "function mcpi_turbo(L=10^9, isinsiderng=isinside)\n",
    "    c = 0\n",
    "    @turbo for i in 1:L\n",
    "        c += isinsiderng(i)\n",
    "    end\n",
    "    4c/L\n",
    "end\n",
    "\n",
    "@time mcpi_turbo()\n",
    "@time mcpi_turbo()\n",
    "@time mcpi_turbo()\n",
    "@time mcpi_turbo()\n",
    "@time mcpi_turbo()\n",
    "@time mcpi_turbo()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "1e563220-0669-4bfe-ab87-50f01f6340e9",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  0.121960 seconds\n",
      "  0.125975 seconds (113 allocations: 7.078 KiB)\n",
      "  0.095930 seconds (4 allocations: 320 bytes)\n",
      "  0.083965 seconds\n",
      "  0.093329 seconds\n",
      "  0.083467 seconds (114 allocations: 7.359 KiB)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "3.141508064"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function mcpi_tturbo(L=10^9, isinsiderng=isinside)\n",
    "    c = 0\n",
    "    @tturbo for i in 1:L\n",
    "        c += isinsiderng(i)\n",
    "    end\n",
    "    4c/L\n",
    "end\n",
    "\n",
    "mcpi_tturbo()\n",
    "@time mcpi_tturbo()\n",
    "@time mcpi_tturbo()\n",
    "@time mcpi_tturbo()\n",
    "@time mcpi_tturbo()\n",
    "@time mcpi_tturbo()\n",
    "@time mcpi_tturbo()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "7b3cd14a-713d-4872-8fae-bde2b6ff4013",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Julia Version 1.12.1\n",
      "Commit ba1e628ee4 (2025-10-17 13:02 UTC)\n",
      "Build Info:\n",
      "  Official https://julialang.org release\n",
      "Platform Info:\n",
      "  OS: Windows (x86_64-w64-mingw32)\n",
      "  CPU: 12 × Intel(R) Core(TM) i7-10750H CPU @ 2.60GHz\n",
      "  WORD_SIZE: 64\n",
      "  LLVM: libLLVM-18.1.7 (ORCJIT, skylake)\n",
      "  GC: Built with stock GC\n",
      "Threads: 12 default, 1 interactive, 12 GC (on 12 virtual cores)\n",
      "Environment:\n",
      "  JULIA_DEPOT_PATH = D:\\.julia\n",
      "  JULIA_NUM_PRECOMPILE_TASKS = 4\n",
      "  JULIA_NUM_THREADS = auto\n",
      "  JULIA_PYTHONCALL_EXE = D:\\.julia\\conda\\3\\python.exe\n"
     ]
    }
   ],
   "source": [
    "versioninfo()"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "encoding": "# -*- coding: utf-8 -*-",
   "formats": "ipynb,jl:hydrogen"
  },
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
